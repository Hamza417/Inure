plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'project-report'
    id 'org.jetbrains.kotlin.android'
}

android {

    compileSdkVersion 33

    buildFeatures {
        viewBinding false
    }

    defaultConfig {
        applicationId "app.simple.inure"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 61
        versionName "build61_final (beta)"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "versionName", versionName

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }


    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    packagingOptions {
        doNotStrip '**/*.so'
    }

    flavorDimensions "version"
    productFlavors {
        github { // GitHub build
            dimension "version"
            // versionNameSuffix "-github"
        }
        play { // Play Store build
            dimension "version"
            // versionNameSuffix "-play"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

            dependenciesInfo {
                includeInApk true
                includeInBundle true
            }
        }
        debug {
            versionNameSuffix "_debug"
            debuggable true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    ndkVersion '24.0.8215888'
    namespace 'app.simple.inure'

    task generateVersionTxt {
        doLast {
            file("./version.txt").text = android.defaultConfig.versionName
        }
    }
    lint {
        disable 'Instantiatable'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.webkit:webkit:1.6.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
    implementation 'androidx.dynamicanimation:dynamicanimation-ktx:1.0.0-alpha03'

    // Google
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.code.gson:gson:2.9.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Toolkit
    implementation 'com.github.android:renderscript-intrinsics-replacement-toolkit:b6363490c3'

    // Third Party
    implementation 'net.dongliu:apk-parser:2.6.10'
    implementation 'com.scottyab:rootbeer-lib:0.0.9'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.bogdwellers:pinchtozoom:0.1'
    implementation "com.anggrayudi:storage:0.13.0"
    implementation 'com.github.duanhong169:drawabletoolbox:1.0.7'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'net.lingala.zip4j:zip4j:2.9.0'
    implementation 'dev.rikka.tools.refine:runtime:3.1.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.jaredrummler:apk-parser:1.0.2'

    // root
    def libsuVersion = '5.0.3'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"

    // Shizuku
    def shizuku_version = '12.2.0'
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"

    // Room
    implementation "androidx.room:room-ktx:2.5.0"
    kapt "androidx.room:room-compiler:2.5.0"
    androidTestImplementation "androidx.room:room-testing:2.5.0"

    // debugImplementation because LeakCanary should only run in debug builds.
    // debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

}
