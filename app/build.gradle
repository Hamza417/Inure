plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'project-report'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'kotlin-kapt'
}

android {

    compileSdk 34

    buildFeatures {
        viewBinding false
        aidl true
    }

    defaultConfig {
        applicationId "app.simple.inure"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 96
        versionName "Build96"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "versionName", versionName

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        javaCompileOptions {
            ksp {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    flavorDimensions.add("version")

    productFlavors {
        github { // GitHub build
            dimension "version"
            // versionNameSuffix "-github"
        }
        play { // Play Store build
            dimension "version"
            applicationIdSuffix ".play"
            // versionNameSuffix "-play"
        }
        beta {
            dimension "version"
            applicationIdSuffix ".beta"
            versionNameSuffix "-test"

            // Remove permissions for beta build
            manifestPlaceholders = [removePermission: "inure.terminal.permission.RUN_SCRIPT"]
            manifestPlaceholders = [removePermission2: "inure.terminal.permission.APPEND_TO_PATH"]
            manifestPlaceholders = [removePermission3: "inure.terminal.permission.PREPEND_TO_PATH"]
        }
    }

    signingConfigs {
        betaRelease {
            def tmpFilePath = System.getProperty("user.home") + "/work/_temp/keystore/"
            def allFilesFromDir = new File(tmpFilePath).listFiles()

            if (allFilesFromDir != null) {
                def keystoreFile = allFilesFromDir.first()
                keystoreFile.renameTo("keystore/key.jks")
            }

            storeFile = file("keystore/key.jks")
            storePassword System.getenv("KEY_STORE_PASSWORD")
            keyAlias System.getenv("ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type. Make sure to use a build
            // variant with `debuggable false`.
            minifyEnabled false

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

            dependenciesInfo {
                includeInApk true
                includeInBundle true
            }
        }
        debug {
            versionNameSuffix "_debug"
            debuggable true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    ndkVersion '25.2.9519653'
    namespace 'app.simple.inure'

    tasks.register('generateVersionTxt') {
        doLast {
            file("./version.txt").text = android.defaultConfig.versionName
        }
    }

    lint {
        disable 'Instantiatable'
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Kotlin
    runtimeOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0' // We use LocalBroadcastManager, Lol!!
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.webkit:webkit:1.9.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta02'
    implementation 'androidx.dynamicanimation:dynamicanimation-ktx:1.0.0-alpha03'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // Google
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.15.1'
    //noinspection KaptUsageInsteadOfKsp
    kapt 'com.github.bumptech.glide:compiler:4.15.1'

    // Toolkit
    implementation 'com.github.android:renderscript-intrinsics-replacement-toolkit:b6363490c3'

    // Third Party
    implementation 'net.dongliu:apk-parser:2.6.10'
    implementation 'com.scottyab:rootbeer-lib:0.1.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.anggrayudi:storage:1.5.4'
    implementation 'com.github.duanhong169:drawabletoolbox:1.0.7'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'net.lingala.zip4j:zip4j:2.11.5'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.jaredrummler:apk-parser:1.0.2'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    // root
    def libsuVersion = '5.1.0'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"

    // Shizuku
    def shizukuVersion = '13.1.0'
    //noinspection GradleDependency
    implementation "dev.rikka.shizuku:api:${shizukuVersion}"
    //noinspection GradleDependency
    implementation "dev.rikka.shizuku:provider:${shizukuVersion}"

    // FDroid
    githubImplementation 'org.fdroid:download:0.1.1'
    githubImplementation 'org.fdroid:index:0.1.1'
    githubImplementation 'org.fdroid:database:0.1.0'

    // Room
    implementation 'androidx.room:room-ktx:2.6.1'
    //noinspection KaptUsageInsteadOfKsp
    kapt 'androidx.room:room-compiler:2.6.1'
    androidTestImplementation 'androidx.room:room-testing:2.6.1'

    // debugImplementation because LeakCanary should only run in debug builds.
    // debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.11'
}
